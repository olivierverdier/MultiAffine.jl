"""
    MultiAffineAction(
      group::MultiAffineGroup,
      selector::AbstractArray,
      conv::ActionDirection=LeftAction()
      )

Given a fixed vector ``S`` of size ``k`` (the `selector`) (or a matrix of size ``k√óm``),
this defines an action of the element ``[X;R]`` of the [`MultiAffineGroup`](@ref) group (so ``X`` is a ``n√ók`` matrix and ``R`` is an element of a matrix group)
 on the vector ``p`` of size ``n`` (or the matrix ``p`` of size ``n√óm``).
The action is defined by
```math
[X;R]‚ãÖp := XS+Rp
```
"""
struct MultiAffineAction{TAD<:ActionDirection,TG,TS<:AbstractArray} <: AbstractGroupAction{TAD}
    group::TG
    selector::TS # vector of length `size`
end

Base.show(io::IO, A::MultiAffineAction{TAD}) where {TAD} = print(io, "MultiAffineAction($(A.group), $(A.selector), $TAD())")

function MultiAffineAction(
    group::MultiAffineGroup{<:Any, <:Any, size},
    selector::AbstractArray,
    conv::ActionDirection=LeftAction()
    ) where {size}
    @assert Base.size(selector, 1) == size
    return MultiAffineAction{typeof(conv), typeof(group), typeof(selector)}(group, selector)
end

"""
    MultiAffineAction(
      group::MultiAffineGroup{<:Any, <:Any, 1},
      conv::ActionDirection=LeftAction()
      )

The standard affine action, obtained with a selector equal to ``[1]``.
"""
function MultiAffineAction(
    group::MultiAffineGroup{<:Any, <:Any, 1},
    conv::ActionDirection=LeftAction()
    )
    return MultiAffineAction(group, [1], conv)
end

function Manifolds.switch_direction(A::MultiAffineAction{TAD}) where {TAD}
    return MultiAffineAction(A.group, A.selector, switch_direction(TAD()))
end


Manifolds.base_group(A::MultiAffineAction) = A.group
Manifolds.group_manifold(::MultiAffineAction{<:Any,<:MultiAffineGroup{<:Any, dim, <:Any, ùîΩ}, <:AbstractVector}) where {dim,ùîΩ} = Euclidean(dim; field=ùîΩ)
Manifolds.group_manifold(A::MultiAffineAction{<:Any,<:MultiAffineGroup{<:Any, dim, <:Any, ùîΩ}, <:AbstractMatrix}) where {dim,ùîΩ} = Euclidean(dim, size(A.selector, 2); field=ùîΩ)

get_selector(A::MultiAffineAction) = A.selector

Manifolds.apply(::MultiAffineAction{<:Any, <:MultiAffineGroup{TH,dim,size,ùîΩ}},
                ::Identity{MultiAffineOp{TH,dim,size,ùîΩ}}, p) where {TH,dim,size,ùîΩ} = p

function Manifolds.apply!(
    A::MultiAffineAction{LeftAction},
    q,
    œá,
    p, # vector of size dim
    )
    G = base_group(A)
    M,R = submanifold_components(G, œá)
    sel = get_selector(A)
    # compute: M*sel + R*p
    LinearAlgebra.mul!(q, M, sel)
    LinearAlgebra.mul!(q, R, p, 1, 1)
    return q
end


Manifolds.apply!(A::MultiAffineAction{RightAction}, q, œá, p) = apply!(switch_direction(A), q, inv(base_group(A), œá), p)



function Manifolds.apply_diff_group(
    A::MultiAffineAction{LeftAction, <:MultiAffineGroup{TH,dim,size,ùîΩ}},
    ::Identity{MultiAffineOp{TH,dim,size,ùîΩ}},
    Œæ,
    p
    ) where {TH,dim,size,ùîΩ}
    G = base_group(A)
    Œº, œâ = submanifold_components(G, Œæ)
    sel = get_selector(A)
    @assert is_point(group_manifold(A), p)
    return Œº*sel + œâ*p
end


function Manifolds.apply_diff_group(
    A::MultiAffineAction{RightAction, <:MultiAffineGroup{TH,dim,size,ùîΩ}},
    I::Identity{MultiAffineOp{TH,dim,size,ùîΩ}},
    Œæ,
    p
    ) where {TH,dim,size,ùîΩ}
    return apply_diff_group(switch_direction(A), I, -Œæ, p)
end
